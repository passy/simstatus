buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.stanfy.android:gradle-plugin-java-robolectric:2.0'
    }
}

apply plugin: 'java-robolectric'

repositories {
    mavenLocal()
    mavenCentral()
}

javarob {
    packageName = 'net.rdrei.android.simstatus'
}

test {
    dependsOn ':simstatus:build'
    scanForTestClasses = false
    include "**/*Test.class"
    // Oh, the humanity!
    def srcDir = project(':simstatus').android.sourceSets.main.java.srcDirs.toArray()[0].getAbsolutePath()
    workingDir srcDir.substring(0, srcDir.lastIndexOf('/'))
}

project(':simstatus').android.sourceSets.main.java.srcDirs.each {dir ->
    def buildDir = dir.getAbsolutePath().split('/')
    buildDir =  (buildDir[0..(buildDir.length - 4)] + ['build', 'classes', 'debug']).join('/')
    println("BuildDIR: " + buildDir)

    sourceSets.test.compileClasspath += files(buildDir)
    sourceSets.test.runtimeClasspath += files(buildDir)
}

dependencies {
    // I don't think I can inherit the dependencies, because they are using incompatible plugins, i.e android vs. java
    compile group: 'com.jakewharton', name: 'butterknife', version: '1.3.2'
    compile group: 'com.squareup.dagger', name: 'dagger', version: '1.0.0'
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile group: 'com.github.kevinsawicki', name: 'http-request', version: '3.0'
    compile group: 'com.bugsense.trace', name: 'bugsense', version: '3.2.2'
    compile group: 'com.google.android.admob', name: 'admob', version: '6.3.1'

    compile group: 'com.google.android', name: 'android', version: '4.1.1.4'

    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'com.squareup', name: 'fest-android', version: '1.0.1'
    testCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    testCompile group: 'org.robolectric', name: 'robolectric', version: '2.0-alpha-3-SNAPSHOT'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '8.0.3.v20111011'
    testCompile group: 'org.easytesting', name: 'fest-assert-core', version: '2.0M10'
}
